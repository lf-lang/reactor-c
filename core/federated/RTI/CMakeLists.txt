# This is a cmake build script providing a solution for compiling
# the RTI in this directory..
#
# Usage:
#
# To compile with cmake, run the following commands:
#
# $> mkdir build && cd build
# $> cmake ../
# $> make
# $> sudo make install
#
# This create a binary RTI in the current working directory. Please put this in
# a directory that is on the path.
#
# To enable DEBUG messages, use the following build commands instead:
#
# $> mkdir build && cd build
# $> cmake -DCMAKE_BUILD_TYPE=DEBUG ../
# $> make
# $> sudo make install
#
# If you would like to go back to non-DEBUG mode, you would have to remove all
# contents of the `build` folder.

# To enable simple HMAC-based authentication of federates,
# add `-DAUTH=ON` option to the cmake command as shown below:
# 
# $> mkdir build && cd build
# $> cmake -DAUTH=ON ../
# $> make
# $> sudo make install
#
# If you would like to go back to non-AUTH mode, you would have to remove all
# contents of the `build` folder.

# To enable SST security of RTI and federates,
# add `-DSST=ON` option to the cmake command as shown below:

# ```bash
# mkdir build && cd build
# cmake -DSST=ON ../
# make
# sudo make install
# ```

# If you would like to go back to non-SST mode, you would have to remove all contents of the `build` folder.

cmake_minimum_required(VERSION 3.12)
project(RTI VERSION 1.0.0 LANGUAGES C)

set(CoreLib ../../../core)

# Check which system we are running on to select the correct platform support
# file and assign the file's path to LF_PLATFORM_FILE
if(${CMAKE_SYSTEM_NAME} STREQUAL "Linux")
  set(LF_PLATFORM_FILE ${CoreLib}/platform/lf_linux_support.c)
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Darwin")
  set(LF_PLATFORM_FILE ${CoreLib}/platform/lf_macos_support.c)
else()
  message(FATAL_ERROR "Your platform is not supported! RTI supports Linux and MacOS.")
endif()

# The following should not be done
include_directories(${CoreLib}/..)
include_directories(${CoreLib})
include_directories(${CoreLib}/federated)
include_directories(${CoreLib}/platform)
include_directories(${CoreLib}/utils)

set(IncludeDir ../../../include/core)
include_directories(${IncludeDir})
include_directories(${IncludeDir}/federated)
include_directories(${IncludeDir}/modal_models)
include_directories(${IncludeDir}/platform)
include_directories(${IncludeDir}/utils)


# Declare a new executable target and list all its sources
add_executable(
    RTI
    rti.c
    ${LF_PLATFORM_FILE}
    ${CoreLib}/platform/lf_unix_clock_support.c
    ${CoreLib}/utils/pqueue.c
    message_record/message_record.c
    ../sst-c-api/c_api.h
)

IF(CMAKE_BUILD_TYPE MATCHES DEBUG)
    # Set the LOG_LEVEL to 4 to get DEBUG messages
    target_compile_definitions(RTI PUBLIC LOG_LEVEL=4)
ENDIF(CMAKE_BUILD_TYPE MATCHES DEBUG)

# Set LF_THREADING to get the threaded support
target_compile_definitions(RTI PUBLIC LF_THREADED=1)

# Find threads and link to it
find_package(Threads REQUIRED)
target_link_libraries(RTI Threads::Threads)

# Option for enabling federate authentication by RTI.
option(AUTH "Federate authentication by RTI enabled." OFF)
# Option for enabling SST security.
option(SST "SST security enabled." OFF)
IF(AUTH MATCHES ON)
  add_compile_definitions(__RTI_AUTH__)
  # Find OpenSSL and link to it
  set(OPENSSL_ROOT_DIR "/usr/local/ssl")
  find_package(OpenSSL REQUIRED)
  target_link_libraries(RTI OpenSSL::SSL)
ENDIF(AUTH MATCHES ON)

IF(SST MATCHES ON)
  add_compile_definitions(__RTI_SST__)
  # Find OpenSSL and link to it
  set(OPENSSL_ROOT_DIR "/usr/local/ssl")
  find_package(OpenSSL REQUIRED)
  target_link_libraries(RTI OpenSSL::SSL)
  include_directories(../sst-c-api)
ENDIF(SST MATCHES ON)


install(
    TARGETS RTI
    DESTINATION bin
)
